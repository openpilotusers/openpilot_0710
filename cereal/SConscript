Import('env', 'arch', 'zmq', 'cython_dependencies')

import shutil

gen_dir = Dir('gen')
messaging_dir = Dir('messaging')
messaging_arne_dir = Dir('messaging_arne')

# TODO: remove src-prefix and cereal from command string. can we set working directory?
env.Command(["gen/c/include/c++.capnp.h", "gen/c/include/java.capnp.h"], [], "mkdir -p " + gen_dir.path + "/c/include && touch $TARGETS")
env.Command(
  ['gen/cpp/car.capnp.c++', 'gen/cpp/log.capnp.c++', 'gen/cpp/arne182.capnp.c++', 'gen/cpp/car.capnp.h', 'gen/cpp/log.capnp.h', 'gen/cpp/arne182.capnp.h'],
  ['car.capnp', 'log.capnp', 'arne182.capnp'],

  'capnpc $SOURCES --src-prefix=cereal -o c++:' + gen_dir.path + '/cpp/')
import shutil
if shutil.which('capnpc-java'):
  env.Command(
    ['gen/java/Car.java', 'gen/java/Log.java', 'gen/java/Arne182.java'],
    ['car.capnp', 'log.capnp', 'arne182.capnp'],
    'capnpc $SOURCES --src-prefix=cereal -o java:' + gen_dir.path + '/java/')

# TODO: remove non shared cereal and messaging
cereal_objects = env.SharedObject([
    'gen/cpp/car.capnp.c++',
    'gen/cpp/log.capnp.c++',
    'gen/cpp/arne182.capnp.c++',
  ])

env.Library('cereal', cereal_objects)
env.SharedLibrary('cereal_shared', cereal_objects)

cereal_dir = Dir('.')
services_h = env.Command(['services.h'],
                          ['service_list.yaml', 'services.py'],
                          'python3 ' + cereal_dir.path + '/services.py > $TARGET')

messaging_objects = env.SharedObject([
  'messaging/messaging.cc',
  'messaging/impl_zmq.cc',
  'messaging/impl_msgq.cc',
  'messaging/msgq.cc',
  'messaging/socketmaster.cc',
])

messaging_arne_objects = env.SharedObject([
  'messaging_arne/messaging.cc',
  'messaging_arne/impl_zmq.cc',
  'messaging_arne/impl_msgq.cc',
  'messaging_arne/msgq.cc',
  'messaging_arne/socketmaster.cc',
])

messaging_lib = env.Library('messaging', messaging_objects)
messaging_arne_lib = env.Library('messaging_arne', messaging_arne_objects)
Depends('messaging/impl_zmq.cc', services_h)
Depends('messaging_arne/impl_zmq.cc', services_h)

# note, this rebuilds the deps shared, zmq is statically linked to make APK happy
# TODO: get APK to load system zmq to remove the static link
if arch == "aarch64":
  zmq_static = FindFile("libzmq.a", "/usr/lib")
  shared_lib_shared_lib = [zmq_static, 'm', 'stdc++', "gnustl_shared", "kj", "capnp"]
  env.SharedLibrary('messaging_shared', messaging_objects, LIBS=shared_lib_shared_lib)
  env.SharedLibrary('messaging_arne_shared', messaging_arne_objects, LIBS=shared_lib_shared_lib)

env.Program('messaging/bridge', ['messaging/bridge.cc'], LIBS=[messaging_lib, 'zmq'])
env.Program('messaging_arne/bridge', ['messaging_arne/bridge.cc'], LIBS=[messaging_lib, 'zmq'])
Depends('messaging/bridge.cc', services_h)
Depends('messaging_arne/bridge.cc', services_h)

# different target?
#env.Program('messaging/demo', ['messaging/demo.cc'], LIBS=[messaging_lib, 'zmq'])


env.Command(['messaging/messaging_pyx.so', 'messaging/messaging_pyx.cpp'],
            cython_dependencies + [messaging_lib, 'messaging/messaging_pyx_setup.py', 'messaging/messaging_pyx.pyx', 'messaging/messaging.pxd'],
            "cd " + messaging_dir.path + " && python3 messaging_pyx_setup.py build_ext --inplace")

env.Command(['messaging_arne/messaging_pyx.so'],
  [messaging_lib, 'messaging_arne/messaging_pyx_setup.py', 'messaging_arne/messaging_pyx.pyx', 'messaging_arne/messaging.pxd'],
  "cd " + messaging_arne_dir.path + " && python3 messaging_pyx_setup.py build_ext --inplace")


if GetOption('test'):
  env.Program('messaging/test_runner', ['messaging/test_runner.cc', 'messaging/msgq_tests.cc'], LIBS=[messaging_lib])

if GetOption('test'):
  env.Program('messaging_arne/test_runner', ['messaging_arne/test_runner.cc', 'messaging_arne/msgq_tests.cc'], LIBS=[messaging_lib])
